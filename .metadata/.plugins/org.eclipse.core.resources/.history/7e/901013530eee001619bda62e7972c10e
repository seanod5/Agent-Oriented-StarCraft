agent Player {
	module EIS eis;
	module Console C;
	module System S;
	module Prelude P;
	
	types model {
		formula currentSupply(int);	//Number of units currently alive
		formula totalSupply(int);	//Number; of units the player can have
		formula unitIDs(list);
	}
	
	initial currentSupply(4);
	initial totalSupply(10);
	initial unitIDs([]);

	rule +!main(list args) {
		boolean matchFlag = false;
		eis.launch("starcraft", "localeisbw-dep.jar");
		eis.join();
		eis.init([map("(2)Astral Balance.scm"), own_race("terran"), starcraft_location("C:\\Program Files (x86)\\StarCraft"), debug("true")]);
		eis.startEnv();
		eis.link("manager");
		C.println("Player ready.");  
		C.println("Free entities: " + eis.freeEntities());
		C.println("All entities: " + eis.allEntities());
		
		list freeEntities = [];
		while(matchFlag == false) {
			freeEntities = eis.freeEntities();	// List will contain a Command Center and four SCVs
			if(P.size(eis.freeEntities()) > 0) {
				matchFlag = true;
			}
		}
		
		P.sort_asc(freeEntities);	// Ensures Command Center is top of list
		S.sleep(2000);
		C.println(P.headAsString(freeEntities));
		S.createAgent(P.headAsString(freeEntities), "TerranCommandCenter");
		
		list SCVs = P.tail(freeEntities);
		
		forall(string ID : SCVs) {
			C.println(ID);
			S.createAgent(ID, "TerranSCV");
			S.sleep(1000);	// Allows for nicer output
		}
		
		while(true) {
			!play();
		}	
	}
	
	rule +!play() {
		list freeEntities = eis.freeEntities();
		forall(string ID : freeEntities) {
			if(eis.getEntityType(ID) == "terranSCV") {
				S.createAgent(ID, "TerranSCV");
			}
			if(eis.getEntityType(ID) == "terranCommandCenter") {
				S.createAgent(ID, "TerranCommandCenter");
			}
		}
	}
	
	rule +!buildSupplyDepot() : currentSupply(int CS) & totalSupply(int TS) {
		list allEntities = eis.allEntities();
		boolean flag = false;
		int i = 0;
		string ID;
		while(flag == false) {
			string ID = P.valueAsString(allEntities, i);
			if(eis.getEntityType(ID) == "terranSCV") {
				flag = true;
			}
			i++;
		}
		
		send(inform, ID, content("supplyDepot"));
	}

		
	rule +$eis.event(resources(int minerals, int gas, int CS, int TS)) : currentSupply(int prevCS) & totalSupply(int prevTS) {
		-currentSupply(prevCS);
		-totalSupply(prevTS);
		+currentSupply(CS);
		+totalSupply(TS);
	}
}
